import java.util.zip.ZipFile

apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.zzzmode.appopsx"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
    }

    aaptOptions {
        noCompress 'jar','sh'
    }

    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }


    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),'proguard-rules.pro'
        }
    }
}

def supportVersion= '1.0.0'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    compileOnly fileTree(include: ['*.jar'], dir: 'deps')
    implementation project(":opsxpro")
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.legacy:legacy-preference-v14:$supportVersion"
    implementation "com.google.android.material:material:1.1.0"
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.github.promeg:tinypinyin:2.0.3'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0'
    implementation project(path: ':apicompat')
}


task providedApiCompat  {

    doLast {

        println("------------- providedApiCompat ")

        new ZipFile("${rootProject.project("apicompat").buildDir}/outputs/aar/apicompat-debug.aar").withCloseable {

            it.getInputStream(it.getEntry("classes.jar")).withStream{
                def ins = it
                def target = file("${project.projectDir}/deps/hidden.jar")
                target.getParentFile().mkdirs()
                target.withOutputStream {
                    it << ins
                }
            }
        }

    }

}


providedApiCompat.dependsOn(':apicompat:assembleDebug')
preBuild.dependsOn(providedApiCompat)


